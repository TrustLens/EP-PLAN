---
layout: default  
title: The EP-PLAN ontology
description: PROV extension for linking Plans and parts of plans to their respective executions
documentation: /EP-PLAN/widoco_output/index-en.html
ontology:https://github.com/TrustLens/EP-PLAN/tree/master/docs/ontologies
pplan: /EP-PLAN/ontologies/p-plan-alignment.xml
---
<body>

     
     
     <h2>Purpose & Intended Use</h2>
      
     <p>The overall purpose of the ontology is to provide a high level model for documenting plans that can be described as a series of sequential inputs and outputs of steps in a form of acyclic graphs. The ontology also covers the modelling of the instantiation of plans (e.g. an execution trace of a workflow), which is achieved via tight alignment with the PROV-O ontology. Such descriptions are useful for documenting the expected and the actual behaviour of complex data processing systems (e.g. an IoT infrastructure), scientific experiments (e.g. an executable scientific workflows), as well as various protocols that are to date predominately stored in its paper form (e.g. a lab protocol of an experiment).</p>  
     <p>The EP-PLAN vocabulary is intended for documenting the expected and actual behaviour of arbitrary systems, however, the plan elements are not strictly designed to guide the execution of processes (e.g. within a scientific workflow platform). The ontology is expected to be useful for scientists and developers, who wish to enhance the transparency of protocols and computational systems through the means of providing meta descriptions, which can be queried by both human users and automated agents. 
     We foresee the main application of the ontology in scenarios where information about systems is sought to facilitate data integration and reuse, pattern mining, and assessments (e.g. trust, privacy, security, quality, etc.). </p>
     
     <h2>License</h2>
     
     <a href="https://creativecommons.org/licenses/by-nc-sa/2.0/">Creative Commons 2.0</a>
     
     <h2>How to cite this resource</h2>
     
     Markovic, M., Garijo, D., Vasconcelos, W., Edwards, P., Gil, Y., EP-PLAN: The vocabulary for associating plan specifications with execution provenance, April 2019, https://w3id.org/ep-plan.  
     
     <h2>References</h2>
     
     TBA
     
      <h2>Overview</h2>
      
     The EP-PLAN ontology builds on two pre-existing ontologies, namely <a href="http://purl.org/net/p-plan">P-PLAN</a>  and <a href="https://www.w3.org/TR/prov-o/">W3C recommendation PROV-O</a>.
     
      <iframe align="center" width="100%" height ="500px" src="webvowl/index.html#"></iframe>
     
     
     
     
      <h2>Examples</h2>
     
      <h3> Sample Datasets  </h3>
     
          <p> See below for available sample datasets </p>
          <ul>   
               <li><a href="/EP-PLAN/examples/sample_datasets/iot/">IoT System Example</a></li>
          </ul>
     
     <h3> Prefixes </h3>
      <table>
      <thead>
      <tr>
      <th>Prefix</th>
      <th>URI</th>
      </tr>
      </thead>
      <tbody>
      <tr>
      <td>ep-plan</td>
      <td>&lt;https://w3id.org/ep-plan></td>
      </tr>
      <tr>
      <td>prov</td>
      <td>&lt;http://www.w3.org/ns/prov#></td>
      </tr>
      <tr>
      <td>odrl</td>
      <td>&lt;http://www.w3.org/ns/odrl/2/></td>
      </tr>
      <td>eli</td>
      <td>&lt;http://data.europa.eu/eli/ontology#></td>
      </tr>
     <td>gdpr</td>
      <td>&lt;https://w3id.org/GDPRtEXT/gdpr#></td>
      </tr>
<td>gdprText</td>
      <td>&lt;https://w3id.org/GDPRtEXT#></td>
      </tr>
      </tbody>
      </table>
      
     <h4> Basic plan structure </h4>
     
     <h5> Example: Using steps and variables to describe plans<h5>
     
     <img src="/EP-PLAN/examples/basicPlan.jpg">
     
     <h4> Mapping execution trace </h4>
     
     <h5> Example: Mapping activities and entities to steps and variables<h5>
     
     <img src="/EP-PLAN/examples/exectrace.jpg">
     
    <h4>  Execution Trace Bundles</h4>
     
     <h5> Example: Keeping everything in one graph <h5>
     
     
     <img src="/EP-PLAN/examples/exectrace2.jpg">
     
    <h5> Example: Using separate documents as bundles <h5>
          
      <img src="/EP-PLAN/examples/exectrace3.jpg">
     
    <h5> Example: Recording the main execution activity<h5>
     
     <img src="/EP-PLAN/examples/exectrace_bundle.jpg">
     
     <h4>  Constraints </h4>
     <h5> Example: Associating constraints with steps<h5>
     <img src="/EP-PLAN/examples/stepConstraint.jpg">
     <h5> Example: Associating constraints with communication specification<h5>
     <img src="/EP-PLAN/examples/commsConstraint.jpg">
     
     <h4> Policies </h4>
     <h5> Example: A policy description expanded using  <href="http://www.w3.org/ns/odrl/2/">ODRL vocabulary</a><h5>
     <img src="/EP-PLAN/examples/policy.jpg">
     
     <h5> Example: A policy linked to a GDPR article><h5>
     <img src="/EP-PLAN/examples/policy2.jpg">
     
     <h4> Plan decomposition </h4>
     
    <h5> Example: Use of MultiVariable<h5>
     <img src="/EP-PLAN/examples/multivariable.jpg">
    
         <h5> Example: Linking executions of different abstractions of a plan><h5>
     <img src="/EP-PLAN/examples/abstractions.jpg">
     
     <h2>  Competency Questions  </h2>     
          <h3> Plan (general structure)</h3>

          <strong>What steps  are part of this plan?</strong>
<pre><code>
SELECT DISTINCT ?step  

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesStep ?step.
     ?step a ep-plan:Step.
    
}
</pre></code>

 <strong>What variables are part of this plan?</strong>
<pre><code>
SELECT DISTINCT  ?variable 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesVar ?variable.
     
     ?variable a ep-plan:Variable.
}
</pre></code>          
          
<strong>What is the order in which steps of this plan should be executed? </strong>

<pre><code>          
SELECT DISTINCT ?step ?nextStep 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesStep ?step; ep-plan:includesStep ?nextStep.
     ?step ep-plan:precedes ?nextStep; a ep-plan:Step.
     ?nextStep a ep-plan:Step.
}
</pre></code>

<strong>Which steps use which variables in this plan ?</strong>
<pre><code>
SELECT DISTINCT ?step ?variableUsed 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesStep ?step; ep-plan:includesVar ?variableUsed.
     ?step a ep-plan:Step; ep-plan:hasInputVar ?variableUsed.
     ?variableUsed a ep-plan:Variable.
}
</pre></code>

<strong>Which steps produce which variable in this plan ?</strong>
<pre><code>
SELECT DISTINCT ?step ?variableProduced 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesStep ?step; ep-plan:includesVar ?variableProduced.
     ?step a ep-plan:Step; ep-plan:hasOutputVar ?variableProduced.
     ?variableProduced a ep-plan:Variable.
}
</pre></code>

<strong>Which variables of this plan were realised during its executions?</strong>
<pre><code>
SELECT DISTINCT ?executionActivity ?entity 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesVar ?variable.
     ?variable a ep-plan:Variable.

     ?executionTrace a ep-plan:ExecutionTraceBundle; prov:wasGeneratedBy ?executionActivity; prov:wasDerivedFrom &lt;PlanURI>; ep-plan:hasTraceElement ?entity.
     ?executionActivity a ep-plan:MultiActivity.

     ?entity a ep-plan:Entity; ep-plan:correspondsToVariable ?variable.
}
</pre></code>


<strong>Which variables of this plan were realised during its executions?</strong>
<pre><code>
SELECT DISTINCT ?executionActivity ?step 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesStep ?failedStep.
     ?failedStep a ep-plan:Step.

     ?executionTrace a ep-plan:ExecutionTraceBundle; prov:wasGeneratedBy ?executionActivity; prov:wasDerivedFrom &lt;PlanURI>; ep-plan:hasTraceElement ?activity. 
     ?executionActivity a ep-plan:MultiActivity.

     ?activity a ep-plan:Activity; ep-plan:correspondsToStep ?step.
}
</pre></code>          
          
          
          
<strong>Which steps of this plan failed to execute?</strong>
<pre><code>
SELECT DISTINCT ?executionActivity ?failedStep

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesStep ?failedStep.
     ?failedStep a ep-plan:Step.

     ?executionTrace a ep-plan:ExecutionTraceBundle; prov:wasGeneratedBy ?executionActivity; prov:wasDerivedFrom &lt;PlanURI>; ep-plan:hasTraceElement ?activity.
     ?executionActivity a ep-plan:MultiActivity.

     ?activity a ep-plan:FailedActivity; ep-plan:correspondsToStep ?failedStep.
}
</pre></code>


          <h3>Decomposition</h3>

<strong>Which steps of this plan are composite steps? </strong>
<pre><code>
SELECT DISTINCT ?step 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesStep ?step.
     ?step a ep-plan:MultiStep.
}
</pre></code>
          
<strong>Which variables of this plan are composite variables?</strong> 
<pre><code>
SELECT DISTINCT ?variable 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesVar ?variable.
     ?variable a ep-plan:MultiVariable.
}
</pre></code>
          
<strong>Which sub-plan of this plan describe which composite step in more detail?</strong> 
<pre><code>
SELECT DISTINCT ?subPlan ?step

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesStep ?step.
     ?step a ep-plan:MultiStep; ep-plan:isDecomposedAsPlan ?subPlan.
     ?subPlan a ep-plan:Plan; ep-plan:isSubPlanOfPlan &lt;PlanURI>.
}
</pre></code>
          
<strong>Which variable of a sub-plan of this plan describe a composite variable of this plan in more detail?</strong>
<pre><code>
SELECT DISTINCT ?compositeVar ?atomicVar 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesVar ?compositeVar.
     ?subPlan a ep-plan:Plan; ep-plan:isSubPlanOfPlan &lt;PlanURI>; ep-plan:includesVar ?atomicVar.
     ?atomicVar a ep-plan:Variable.
     ?compositeVar a ep-plan:MultiVariable; ep-plan:hasPart ?atomicVar.
}
</pre></code>

<strong>Which variables of this plan is also used in a sub-plan of this plan?</strong>
<pre><code>
SELECT DISTINCT ?variable ?subplan 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesVar ?variable.
     ?subPlan a ep-plan:Plan; ep-plan:isSubPlanOfPlan &lt;PlanURI>; ep-plan:includesVar ?variable.
     ?variable a ep-plan:Variable.
}
</pre></code>
<pre><code>
<strong>Which realisation of variables of a sub-plan relate to realisations of composite variables of this plan?</strong> 

SELECT DISTINCT ?compositeEntity ?atomicEntity 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesVar ?compositeVar.
     ?subPlan a ep-plan:Plan; ep-plan:isSubPlanOfPlan &lt;PlanURI>; ep-plan:includesVar ?atomicVar.
     ?atomicVar a ep-plan:Variable.
     ?compositeVar a ep-plan:MultiVariable; ep-plan:hasPart ?atomicVar.

     ?mainExecutionTrace a ep-plan:ExecutionTraceBundle; prov:wasDerivedFrom &lt;PlanURI>; ep-plan:hasTraceElement ?compositeEntity; ep-plan:hasTraceElement ?subPlanExecutionTrace.
     ?subPlanExecutionTrace a ep-plan:ExecutionTraceBundle; ep-plan:hasTraceElement ?atomicEntity; prov:wasDerivedFrom ?subPlan.

     ?compositeEntity a ep-plan:EntityCollection; ep-plan:correspondsToVariable ?compositeVar; prov:hadMember ?atomicEntity.
     ?atomicEntity a ep-plan:Entity; ep-plan:correspondsToVariable ?atomicVar. 
}
</pre></code>


<strong>Which realisation of variables of this plan also belong to the execution of its sub-plan?</strong> 
<pre><code>
SELECT DISTINCT ?mainPlanEntity ?subPlanEntity 

WHERE
{
     &lt;PlanURI> a ep-plan:Plan; ep-plan:includesVar ?variable.
     ?subPlan a ep-plan:Plan; ep-plan:isSubPlanOfPlan &lt;PlanURI>; ep-plan:includesVar ?variable.
     ?variable a ep-plan:Variable.

     ?mainExecutionTrace a ep-plan:ExecutionTraceBundle; prov:wasDerivedFrom &lt;PlanURI>; ep-plan:hasTraceElement ?mainPlanEntity; ep-plan:hasTraceElement ?subPlanExecutionTrace.
     ?subPlanExecutionTrace a ep-plan:ExecutionTraceBundle; ep-plan:hasTraceElement ?subPlanEntity; prov:wasDerivedFrom ?subPlan.

     ?mainPlanEntity a ep-plan:Entity; ep-plan:correspondsToVariable ?variable.
     ?subPlanEntity a ep-plan:Entity; ep-plan:correspondsToVariable ?variable; prov:mentionOf ?mainPlanEntity. 
}
</pre></code>

          <h3>Agency</h3>

<strong>Which agent was responsible for the overall execution of this plan that generated which execution trace?</strong>
<pre><code>
SELECT DISTINCT ?agent ?executionTrace

WHERE 
{
     &lt;PlanURI> a ep-plan:Plan.
     ?executionTrace a ep-plan:ExecutionTraceBundle; prov:wasGeneratedBy ?executionActivity.
     ?executionActivity prov:wasAssociatedWith ?agent; a ep-plan:MultiActivity.

     ?executionTrace prov:wasDerivedFrom &lt;PlanURI>.
     
}
</pre></code>     
          
     
     <strong>Which agents are allowed to participate in this plan?</strong>
         
     <pre><code>
     SELECT DISTINCT ?agent 
     
     WHERE {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesResponsibleAgent ?agent.
     }
     </code></pre>    
     * If the identity of agents is not known and the agent's ability to participate in plan execution is based on certain characteristic (e.g. must be at certain location) such restrictions can be modelled as a constraint.   
     <br>
     <strong>Which agents are allowed to participate in this plan (search all abstraction layers)?</strong>
         
     <pre><code>
     SELECT DISTINCT ?agent 
     
     WHERE {
     {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesResponsibleAgent ?agent.
     }
     UNION {
          ?subPlan ep-plan:isSubPlanOfPlan* &lt;PlanURI>; ep-plan:includesResponsibleAgent ?agent.
     }
     }
     </code></pre>    
     
     <strong>Which agents are allowed to participate in execution of  this step?</strong>
     <pre><code>
     SELECT DISTINCT ?agent 
     
     WHERE {
     
          &lt;stepURI> a ep-plan:Step; ep-plan:canBeResponsibilityOf ?agent.
     }
     </code></pre>
         
     <strong>Which agents are allowed to participate in execution of this MultiStep (search all abstraction layers)?</strong>
     <pre><code>
     SELECT DISTINCT ?agent 

     WHERE {
     {
          &lt;StepURI> a ep-plan:MultiStep; ep-plan:canBeResponsibilityOf ?agent.
     }
     UNION
     {
          &lt;StepURI> ep-plan:isDecomposedAsPlan ?plan.
          {
          ?plan a ep-plan:Plan; ep-plan:includesResponsibleAgent ?agent.
          }
          Union
          {
          ?subPlan ep-plan:isSubPlanOfPlan* ?plan; ep-plan:includesResponsibleAgent ?agent.
          }
     }
     }
     </code></pre>
          
     <strong>Which agents that were allowed to participate in in which execution of this step participated in the actual execution of this step?</strong>
     <pre><code>
     SELECT DISTINCT  ?agent ?activity
     
     WHERE {
     
          &lt;stepURI> a ep-plan:Step; ep-plan:canBeResponsibilityOf ?agent.
          ?agent a ep-plan:ResponsibleAgent; a ep-plan:Agent.
          ?activity a ep-plan:Activity; ep-plan:correspondsToStep &lt;stepURI>; prov:wasAssociatedWith ?agent.
     }
     </code></pre>
    
    <h3>Constraints</h3>
    
    <strong>What constraints are associated with the steps of this plan?</strong>
     <pre><code>
     SELECT DISTINCT ?constraint ?step 

     WHERE 
     {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesConstraint ?constraint; ep-plan:includesStep ?step.
          ?step ep-plan:hasConstraint ?constraint; a ep-plan:Step.
          ?constraint a ep-plan:Constraint.
     }
     </code></pre>
     
     <strong>Which executions of this planned step satisfied which constraints?</strong>
     <pre><code>
     SELECT DISTINCT ?constraint ?activity 

     WHERE 
     {
          &lt;PlanURI> ep-plan:includesConstraint ?constraint; ep-plan:includesStep ?step.
          ?step ep-plan:hasConstraint ?constraint; a ep-plan:Step.
          ?constraint a ep-plan:Constraint.
          ?activity ep-plan:correspondsToStep ?step; a ep-plan:Activity.
          ?activity ep-plan:satisfied ?constraint.
          ?execTraceBundle prov:wasDerivedFrom  &lt;PlanURI>; ep-plan:hasTraceElement ?activity.
     }
     
     
     <p>Note: if there are no more than one plan descriptions present in the graph and the URIs of constraint and step element have not been reused as part of different plans, then first and last line of the query can be omitted.</p>
     </code></pre>
     <strong>Which executions of this planned step violated which constraints?</strong>
     <pre><code>
     SELECT DISTINCT ?constraint ?activity 

     WHERE 
     {
          &lt;PlanURI> ep-plan:includesConstraint ?constraint; ep-plan:includesStep ?step.
          ?step ep-plan:hasConstraint ?constraint; a ep-plan:Step.
          ?constraint a ep-plan:Constraint.
          ?activity ep-plan:correspondsToStep ?step; a ep-plan:Activity.
          ?activity ep-plan:violated ?constraint.
          ?execTraceBundle prov:wasDerivedFrom  &lt;PlanURI>; ep-plan:hasTraceElement ?activity.
     }
     </code></pre>
     
     <strong>What constraints are restricting the exchange of data between steps in this plan?
</strong>
     <pre><code>
     SELECT DISTINCT ?constraint ?commsSpec 

     WHERE 
     {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesCommsSpec ?commsSpec; ep-plan:includesStep ?step.
          ?commsSpec ep-plan:hasConstraint ?constraint; a ep-plan:CommsSpec.
          ?constraint a ep-plan:Constraint.
     }
     </code></pre>
         
     <strong>Exchange of which data variable between which steps is restricted which constraint in this plan?
</strong>
     <pre><code>
    SELECT DISTINCT ?constraint ?variable 

     WHERE 
     {
          &lt;PlanURI>  a ep-plan:Plan; ep-plan:includesCommsSpec ?commsSpec; ep-plan:includesStep ?step1; ep-plan:includesStep ?step2; ep-plan:includesVar ?variable.
          ?commsSpec ep-plan:hasConstraint ?constraint; a ep-plan:CommsSpec.
          ?constraint a ep-plan:Constraint.
          ?commsSpec ep-plan:hasSender ?step1; ep-plan:hasRecipient?step2; ep-plan:hasPayload ?variable.
          ?variable a ep-plan:Variable.
          ?step1 a ep-plan:Step.
          ?step2 a ep-plan:Step.
     }
     </code></pre>
     
     <strong>Which step executions satisfied constraints restricting the exchange of data in this plan?
</strong>
     <pre><code>
    SELECT DISTINCT ?constraint ?senderActivity ?recipientActivity ?exchangedData

     WHERE 
     {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesCommsSpec ?commsSpec; ep-plan:includesStep ?sender; ep-plan:includesStep ?recipient; ep-plan:includesVar ?variable.

          ?senderActivity a ep-plan:Activity; ep-plan:correspondsToStep ?sender.
          ?exchangedData a ep-plan:Entity; prov:wasGeneratedBy ?senderActivity; ep-plan:correspondsToVariable ?variable. 
          ?recipientActivity a ep-plan:Activity; prov:used ?exchangedData; prov:qualifiedCommunication ?communication;ep-plan:correspondsToStep ?recipient.
          ?communication a ep-plan:Communication; ep-plan:correspondsToCommsSpec ?commsSpec; prov:activity ?senderActivity.
          
          ?commsSpec ep-plan:hasConstraint ?constraint; a ep-plan:CommsSpec.
          ?commsSpec ep-plan:hasSender ?sender; ep-plan:hasRecipient ?recipient; ep-plan:hasPayload ?variable.
          ?communication ep-plan:satisfied ?constraint.
     }
     </code></pre>
     
      <h3>Policies</h3>
     
     <strong>What are the policies associated with this plan?</strong>
     <pre><code>
     SELECT DISTINCT ?policy

     WHERE 
     {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesPolicy ?policy. 
          ?policy a ep-plan:Policy.
     }
     </code></pre>
     
     <strong>Which plan elements were included in this plan as a result of a policy?
</strong>
     <pre><code>
     SELECT DISTINCT ?policy ?planElement

     WHERE 
     {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesPolicy ?policy; ep-plan:includesPlanElement ?planElement. 
          ?policy a ep-plan:Policy; a ep-plan:Rationale.
          ?planElement ep-plan:hasRationale ?policy.
     }
     </code></pre>
     
     <h3>Rationale & Objectives</h3>
     
     <strong>Why was this policy associated with this plan?
</strong>
     <pre><code>
     SELECT DISTINCT ?policyRationale

     WHERE 
     {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesPolicy &lt;PolicyURI>.
          &lt;PolicyURI> a ep-plan:Policy; ep-plan:hasRationale ?policyRationale. 
          ?policyRationale a ep-plan:Rationale. 
     }
     </code></pre>
     <strong>What are the objectives of this plan and what is their rationale (if known)?
</strong>
     <pre><code>
     SELECT DISTINCT ?objective

     WHERE 
     {
          &lt;PlanURI> a ep-plan:Plan; ep-plan:includesObjective ?objective.
          ?objective a ep-plan:Objective. 
          OPTIONAL {
          ?rationale a ep-plan:Rationale; ep-plan:isRationaleOf  ?objective.
          }
     }
     </code></pre>
     
  </body>    
      
      
      
      
      

 
